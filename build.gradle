plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	maven { url = "https://api.modrinth.com/maven" }
	maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
	maven { url = "https://mvn.devos.one/releases" } // Porting Lib releases
	maven { url = "https://mvn.devos.one/snapshots" } // Create and several dependencies
	maven { url = "https://modmaven.dev/" }
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
	maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven" } // Forge Config API Port

	maven { // Fabric ASM for Porting Lib
		url = "https://jitpack.io/"
		content { includeGroupAndSubgroups("com.github") }
	}

	maven { url = "https://maven.shedaniel.me" } // Cloth Config, REI
	maven { url = "https://maven.blamejared.com" } // JEI

	maven { url = "https://maven.terraformersmc.com/releases" } // Mod Menu, EMI
}

configurations.configureEach {
	resolutionStrategy {
		// Make sure the desired version of loader is used. 
		// Sometimes old versions are pulled in transitively.
		force("net.fabricmc:fabric-loader:$fabric_loader_version")
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings(loom.layered {
		it.officialMojangMappings { nameSyntheticMembers = false }
		it.parchment("org.parchmentmc.data:parchment-$minecraft_version:$parchment_version@zip")
	})

	// Fabric Loader
	modImplementation("net.fabricmc:fabric-loader:$fabric_loader_version")

	// Fabric API
	modImplementation("net.fabricmc.fabric-api:fabric-api:$fabric_api_version")

	// Mod Menu
	modCompileOnly("com.terraformersmc:modmenu:$modmenu_version")

	// Sodium
	modCompileOnly("maven.modrinth:sodium:${project.sodium_version}")

	// Distant Horizons
	modCompileOnly("maven.modrinth:distanthorizonsapi:${project.distant_horizons_api_version}")
	modCompileOnly("maven.modrinth:distanthorizons:${project.distant_horizons_version}")

	// Create
	modCompileOnly("com.simibubi.create:create-fabric-$minecraft_version:$create_version")

	// Create: Deco
	modCompileOnly("maven.modrinth:create-deco:${project.create_deco_version}")

	// TODO: Add Sodium to deps, ask Codex to check entity render distance via decompile, implement limiter of render distance - 2 chunks.
	
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}